plugins {
    id 'org.kordamp.gradle.jacoco' version '0.54.0'
    id 'io.spring.dependency-management' version '1.1.4'
}

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'jacoco'

    group = 'oopp'
    version = '0.0.1-SNAPSHOT'

    checkstyle {
        toolVersion = '10.13.0'

        ignoreFailures = false
        maxErrors = 0
        maxWarnings = 0
        configFile = '../checkstyle.xml' as File
    }

    tasks.named('test') {
        useJUnitPlatform()
        finalizedBy jacocoTestReport
    }

    jacocoTestReport {
        reports {
            xml.required = false
            csv.required = false
            html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
        }
    }

    // https://github.com/gradle/gradle/issues/27035
    configurations.checkstyle {
        resolutionStrategy.capabilitiesResolution.withCapability('com.google.collections:google-collections') {
            select('com.google.guava:guava:0')
        }
    }

    dependencies {
        implementation 'org.apache.commons:commons-lang3:3.14.0'

        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
        testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.10.2'
        testImplementation 'org.mockito:mockito-core:5.10.0'
    }
}